*** Settings ***
Resource          Resources/Common_Library.txt
Resource          Resources/Common.txt

*** Keywords ***
User is in Todo Page
    Click Link    Todo

Should List Recent 5 Todo Items
    Comment    Query Top 5 Todos
    @{query_results}    Query    select item, date from todo where owner = ${USER_ID} order by date desc limit 5
    Comment    Reformat the dates
    ${query_todo_item}    ${query_todo_dates}=    Split By Columns    ${query_results}
    @{query_todo_dates_formatted}=    Convert Date Format    ${query_todo_dates}    %Y-%m-%d %H:%M:%S.%f    %B %d, %Y
    @{todos_from_query}=    Combine By Columns    ${query_todo_item}    ${query_todo_dates_formatted}
    Comment    Get Todo items in Web Page
    @{todo_items_from_webpage}=    Get All Text    xpath=//div[contains(@class, '_todo_item')]//span[contains(@class, 'ng-binding')]
    @{todo_dates_from_webpage}=    Get All Text    xpath=//div[contains(@class, '_todo_date')]//span[contains(@class, 'ng-binding')]
    @{todos_from_webpage}=    Combine By Columns    ${todo_items_from_webpage}    ${todo_dates_from_webpage}
    Comment    Compare Todo items from Query vs Todo items from Web Page
    Should Be Equal    ${todos_from_query}    ${todos_from_webpage}
