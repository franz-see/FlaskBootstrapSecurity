*** Settings ***
Resource          Resources/Common_Library.txt
Resource          Security_Resource.txt
Resource          Resources/Common.txt

*** Variables ***
${INCORRECT_USERNAME}    wrong@username.com
${INCORRECT_PASSWORD}    wrong password
${NEW_USERNAME}    replaced in Init Variables
${NEW_PASSWORD}    new password
${INVALID_NEW_PASSWORD}    12345    # Less than 6 characters

*** Keywords ***
User is in Login Page
    Click Link    Login

User Should Be Logged In
    Page Should Contain    Logout

User Should NOT Be Logged In
    Page Should Not Contain    Logout

User is in Registration Page
    Click Link    Register

User Registers With
    [Arguments]    ${username}    ${password}
    Input Text    email    ${username}
    Input Text    password    ${password}
    Click Button    Register

User Should be able to Login With
    [Arguments]    ${username}    ${password}
    User Logs In With    ${username}    ${password}
    User Should Be Logged In

Init Variables
    ${NEW_USERNAME}=    Evaluate    "test" + str(random.randint(0, 1000)) + "@example.com"    random
    Set Test Variable    ${NEW_USERNAME}    ${NEW_USERNAME}

User is in Change Password
    Click Element    xpath=//a[starts-with(text(), "Hello") and contains(text(), "(Profile)")]

Password should have changed from _ to _
    [Arguments]    ${username}    ${old_password}    ${new_password}
    User Logs In With    ${username}    ${old_password}
    Page Should Contain    Incorrect Username or Password
    User Should be able to Login With    ${username}    ${new_password}

Create New User
    Init Variables
    Logout if Possible
    User is in Registration Page
    User Registers With    ${NEW_USERNAME}    ${USER_PASSWORD}
    Logout if Possible

User Changes Password With
    [Arguments]    ${current_password}    ${new_password}    ${confirm_password}
    Input Password    password    ${current_password}
    Input Password    new_password    ${new_password}
    Input Password    new_password_confirm    ${confirm_password}
    Click Button    Change Password
